{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "5ae71ca6-6570-4e52-b297-73b3e7cc38bd",
   "metadata": {},
   "source": [
    "# PWC348, Task 2 Convert Time\n",
    "**冯昶 2025-11-17**\n",
    "\n",
    "First, let's define a method to translate hh:mm format time into minutes:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "1c345998-99f1-47d6-a332-897851cd23b5",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "1\n",
      "60\n"
     ]
    }
   ],
   "source": [
    "my method time-to-min(Str:D $time : --> UInt:D) {\n",
    "    .substr(0,2) * 60 + .substr(3) with $time\n",
    "}\n",
    "put '00:01'.&time-to-min;\n",
    "put '01:00'.&time-to-min;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "1b3cf855-3933-4769-a1ce-33465485c61c",
   "metadata": {},
   "outputs": [],
   "source": [
    "Then, an introduction to the `polymod()` function.\n",
    "\n",
    "polymod() is effective in translating numbers into units, e.g.:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "id": "72b3a2c6-0b62-48f9-b279-d8dab6985de8",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "(39, 46, 3, 1).Seq\n",
      "(5, 4, 3, 2, 1).Seq\n"
     ]
    }
   ],
   "source": [
    "# translate 99999 seconds into number of seconds, minutes, hours, and days\n",
    "put 99999.polymod(60,60,24).raku;       # 1 day, 3 hours, 46 minutes, and 39 seconds\n",
    "\n",
    "# translate 12345 into digits:\n",
    "put 12345.polymod(10 xx 4).raku;"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c7513b54-90b2-4905-b814-dc772d6f9077",
   "metadata": {},
   "source": [
    "Here is my raku script for task 2:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "09745293-1359-428b-ab3a-226a74c946f3",
   "metadata": {},
   "outputs": [],
   "source": [
    "unit sub MAIN(Str:D $source, Str:D $target);\n",
    "\n",
    "my method time-to-min(Str:D $time : --> UInt:D) {\n",
    "    .substr(0,2) * 60 + .substr(3) with $time\n",
    "}\n",
    "\n",
    "my $diff = $target.&time-to-min - $source.&time-to-min;\n",
    "$diff += 60 * 24 if $diff < 0;       # add a day to the time difference when target is from the 2nd day\n",
    "put $diff.polymod(5, 3, 4).sum;      # first unit is 5min, 2nd unit is 5*3=15min, 3rd unit is 15*4=60min"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Raku",
   "language": "raku",
   "name": "raku"
  },
  "language_info": {
   "file_extension": ".raku",
   "mimetype": "text/plain",
   "name": "raku",
   "version": "6.d"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
