{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "5ae71ca6-6570-4e52-b297-73b3e7cc38bd",
   "metadata": {},
   "source": [
    "##### PWC350, Task 2 Shuffle Pairs\n",
    "**冯昶 2025-12-08**\n",
    "\n",
    "To test if 2 different numbers have the same digits, we can reorder all the digits of each number from low to high, and test if the results are equal.\n",
    "Note we test equalness with 'eq' instead of '==' to avoid pit falls."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "c2d380f5-34c2-46dc-ac13-26df121f7cf0",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "12345\n",
      "True\n",
      "105\n",
      "015\n",
      "False\n",
      "True\n"
     ]
    }
   ],
   "source": [
    "put '13254'.comb.sort.join;                # 12345\n",
    "put '13254'.comb.sort.join eq '12345';     # True\n",
    "\n",
    "put 15*7;                                  # 15 * 7 = 105\n",
    "put (15*7).comb.sort.join;                 # 015\n",
    "put (15*7).comb.sort.join eq '15';         # False, '015' and '15' are different\n",
    "put (15*7).comb.sort.join == 15;           # True, 015 equals 15 as numbers"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "d6ea65c5-4348-49d7-891a-3beff2a6ad9e",
   "metadata": {},
   "source": [
    "Here is my script for Task 2:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ba5bd9fc-6ffa-4fff-8ec6-d67fe55815df",
   "metadata": {},
   "outputs": [],
   "source": [
    "unit sub MAIN(UInt:D $from, UInt:D $to, UInt:D $count);\n",
    "\n",
    "put +($from..$to).grep(-> $n {\n",
    "    my $b = $n.comb.sort.join;\n",
    "    +(2..9).grep({ ($n*$_).comb.sort.join eq $b }) ≥ $count\n",
    "});"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "ffa27a15-99b9-4567-b488-c44410071fb9",
   "metadata": {},
   "source": [
    "We test 2 through 9 times of each candidate `$n`, whether the result is a shuffle pair of `$n`.\n",
    "Each number with no less than $count shuffle pairs is recorded.\n",
    "Finally, we print the counts of numbers that were recorded."
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Raku",
   "language": "raku",
   "name": "raku"
  },
  "language_info": {
   "file_extension": ".raku",
   "mimetype": "text/plain",
   "name": "raku",
   "version": "6.d"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
