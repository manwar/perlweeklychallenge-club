 MaxDiff←{

     _←'cmat' 'pmat'⎕CY'dfns'
     MD1←{
         {⌈/-/×/(≢⍵)2 2⍴⍵},[1 2](⍵[4 cmat≢⍵]){⍺[⍵]}⍤1 99⊢pmat 4
     }

     MD2←{
         ⌈/-/×/⍵[c⌷⍨⊂↑⍸(⊢≡∪)∘∊¨∘.(,⍥⊂)⍨↓c←2 cmat≢⍵]
     }

     Pairs←{↑⊃,/i,¨¨(i←¯1↓⍳⍵)↓¨⊂⍳⍵}
     MD←{
         p←Pairs≢⍵ ⋄ rm←Pairs ¯2+≢⍵
         ⌈/,-/×/⍵⌷⍨⊂p,[0.5]⍤1⍤1 2⊢rm⌷⍤0 1⍤2 1⊢(⍳≢n)~⍤1⊢p
     }

⍝ Tests
     42≢MD 5 9 3 4 6:∘∘∘
     10≢MD 1 ¯2 3 ¯4:∘∘∘
     10≢MD ¯3 ¯1 ¯2 ¯4:∘∘∘
     50≢MD 10 2 0 5 1:∘∘∘
     44≢MD 7 8 9 10 10:∘∘∘

     'All tests passed.'

 }
