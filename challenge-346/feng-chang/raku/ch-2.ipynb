{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "73898474-bf47-4a53-96a0-3673eb0014a6",
   "metadata": {},
   "source": [
    "# PWC346, Task 2 Magic Expression\n",
    "**冯昶 2025-11-11**\n",
    "\n",
    "Following is my ch-2.raku script:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ff38da0a-6e06-4505-9de4-44065347e725",
   "metadata": {},
   "outputs": [],
   "source": [
    "#!/bin/env raku\n",
    "\n",
    "unit sub MAIN(Str:D $s, Int:D $tgt);\n",
    "\n",
    "use MONKEY-SEE-NO-EVAL;\n",
    "\n",
    "my @s = $s.comb;\n",
    "my @opers = '+', '-', '*', '';\n",
    "\n",
    "my @ans;\n",
    "for [X] @opers xx (+@s-1) -> @ops {\n",
    "    my $exp = roundrobin(@s, @ops).flat.join;\n",
    "    next if $exp ~~ /« '0' \\d/;    # drop expressions which contains number with a leading 0\n",
    "    @ans.push($exp) if EVAL($exp) == $tgt;\n",
    "}\n",
    "put @ans.join(', ');"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "27f89c89-9fd2-4d02-ae80-2415a2905ee3",
   "metadata": {},
   "source": [
    "Let me explain the code line by line."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 10,
   "id": "5731b860-2f98-407a-bcdd-e848c4054bd2",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "[\"1\", \"2\", \"3\", \"4\"]\n"
     ]
    }
   ],
   "source": [
    "# $s.comb translates string $s into list of numbers\n",
    "my $s = '1234';\n",
    "my @s = $s.comb;\n",
    "put @s.raku;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "62c52ba5-81d8-4d2e-8a96-99f79ee86379",
   "metadata": {},
   "outputs": [],
   "source": [
    "# @opers array consists of all possible operations\n",
    "my @opers = '+', '-', '*', '';\n",
    "# of which the '' op is the operation to join 2 numbers together"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 11,
   "id": "034dbc9e-b85e-4608-91e7-f5651c57c293",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "4\n"
     ]
    }
   ],
   "source": [
    "# +@s is the size of array @s, while +@s is size of @s minus 1\n",
    "my @s = <1 2 3 4 5>;\n",
    "put +@s-1;"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "2f58fc51-bdda-4ec0-a0c5-f5ae7863f848",
   "metadata": {},
   "source": [
    "`[X] @opers xx 3` is the same as `@opers X @opers X @opers`.\n",
    "So @ops in the for loop iterates through all possible operation combinations.\n",
    "\n",
    "roundrobin() combines the digits and operations together. e.g.,"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 15,
   "id": "4e252fee-fff3-4d67-9ecd-ed067a507441",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "1+2-3*4+5\n"
     ]
    }
   ],
   "source": [
    "put roundrobin(<1 2 3 4 5>, <+ - * +>).flat.join;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "7c9588c5-4032-4427-96ac-a4f1eb596544",
   "metadata": {},
   "outputs": [],
   "source": [
    "# the « sign in regex is left anchor for a word\n",
    "next if $exp ~~ /« '0' \\d/;    # drop expressions which contains number with a leading 0"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "3a3b76bb-49a6-4eba-a4d0-9b2074c62809",
   "metadata": {},
   "outputs": [],
   "source": [
    "# at the end of the loop, @ans will contain all answers\n",
    "@ans.push($exp) if EVAL($exp) == $tgt;\n",
    "\n",
    "`use MONKEY-SEE-NO-EVAL;` must be declared because EVAL() is considered unsafe.\n",
    "\n",
    "The last line prints all answers separated by commas:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "23ea10e4-8fa0-4654-ad89-c6495f451f2b",
   "metadata": {},
   "outputs": [],
   "source": [
    "put @ans.join(', ');"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Raku",
   "language": "raku",
   "name": "raku"
  },
  "language_info": {
   "file_extension": ".raku",
   "mimetype": "text/plain",
   "name": "raku",
   "version": "6.d"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
